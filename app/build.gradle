apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'org.greenrobot.greendao'


File signPropertiesFile = rootProject.file('sign/keystore.properties')
android {
    productFlavors {
        baidu {}
        _91 {}
        wandoujia {}
        xiaomi {}
        qh360 {}
        kuan {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    compileSdkVersion 25
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.omg.ireader"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    if (signPropertiesFile.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(signPropertiesFile))
        signingConfigs {
            release {
                storeFile rootProject.file(properties['keystore'])
                storePassword properties['storePassword']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexEnabled true
            if (signPropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.all { variant ->

        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {

                // Rename APK
//            def name = "${versionName}.apk"
                def fileName = "ireader_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                output.outputFile = new File(output.outputFile.parent, fileName)
            }
        }
    }
}


greendao {
    schemaVersion 2
    daoPackage 'com.omg.ireader.model.gen'
    targetGenDir 'src/main/java'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    //View injection
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor "com.jakewharton:butterknife-compiler:8.8.1"
    //RxJava
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    //NetWork
    compile 'com.squareup.okhttp3:okhttp:1.11.0'
    compile 'com.squareup.okio:okio:1.11.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    //ImageLoader
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //ORM Database
    compile 'org.greenrobot:greendao:3.2.0'
    testCompile 'junit:junit:4.12'
    //AndroidTagGroup
    compile 'me.gujun.android.taggroup:library:1.4@aar'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha9'
    //download file breakpoint resume
    compile 'com.liulishuo.filedownloader:library:1.6.7'
    compile 'cn.pedant.sweetalert:library:1.3'
    compile files('libs/umeng-analytics-7.4.0.jar')
    compile files('libs/umeng-common-1.4.0.jar')
    compile files('libs/GDTUnionSDK.4.12.553.min.jar')
}

